%%%%%%
% Model of a Logical Hidden Markov Model (LOHMM)
% LM = (SIGMA, MU, DELTA, UPSILON)
% Where
%     SIGMA := logical alphabet
%     MU := selection (grounding) probability over SIGMA
%     DELTA := set of abstract transitions T = (p : H <- B emitting O)
%     UPSILON := set of abstract transitions encoding a prior distribution of states
% ---
% USAGE:
%     ?- sample(lohmm(Observations)).
%     Returns a list of observations emitted from the LOHMM
%
%     ?- sample(lohmm(States, Observations)).
%     Returns a list of observations emitted and states traversed via the LOHMM
%%%%%%

%%------------------------------------
%% Modeling section
%%------------------------------------

%%
% [lohmm/1] lohmm(Observations)
% ---
% Observations := a list of observations emitted from the LOHMM
% ---
% Returns the list of observations.
%%
lohmm(Obs) :- lohmm(_, Obs).

%%
% [lohmm/2] lohmm(States, Observations)
% ---
% States := a list of states traversed
% Observations := a list of observations emitted from the LOHMM
% ---
% Returns the list of states and observations.
%%
lohmm(States, Obs) :-                 
    lohmm(start, [start | States], [_ | Obs]). 

%%
% [lohmm/3] lohmm(Current, States, Observations)
% ---
% Current := the current ground state
% States := a list of states traversed
% Observations := a list of observations emitted from the LOHMM
% ---
% Selects trans/3 based on the current state,
%     IDs are generated for the partially ground obs and next,
%     using these IDs, obs and next are ground,
%     loop until the current state is stop.
%%
lohmm(State, [State|States], [Obs|Obss]) :- 
    trans(State, Obs, Next),
    (  Next = stop
    -> Obss = [], !
    ;  generateID(Obs, ID1),  
       generateID(Next, ID2),  
       bind(Obs, ID1),  
       bind(Next, ID2),
       lohmm(Next, States, Obss)
    ).

%%
% [trans/3] trans(Current, Obs, Next)
% ---
% Current := the current ground state
% Obs := the partially ground observation
% Next := the partially ground next state
% ---
% First selects a RuleID (to implement subsumption constraint),
%     then uses the DELTA transitions of Rule_ID to select the next Rule,
%     unifies rule/3 based on the current state and the selected Rule.
%
% NOTE: Rule itself is also an identifier used so that all instances which may
% unify with some msw will share the same transition probability space for learning.
%%
trans(State, Obs, Next) :-
    msw(State, RuleID),         
    msw(RuleID, Rule),         
    rule(Rule, State, (Obs, Next)).  

%%
% [bind/2] bind(Term, ID)
% ---
% Term := patially ground term
% ID := ID generated for the term
% ---
% Accesses Term arity and arguments and calls allbind/3 to ground the term.
%%
bind(Term, ID) :- 
    functor(Term, F, N),         
    Term =.. [F|Args],             
    allbind(F/N, 1, Args, ID). 

%%
% [allbind/4] allbind(Term, I, Args, ID)
% ---
% Term := term represented by <term_name>/<arity>, used to index msw
% I := current iteration which corresponds to the current arg position
% Args := list of arguments in term
% ID := generated ID of term
% ---
% Loop over Arg in Args:
%     if Arg is not ground:
%         ground Arg via MU
%
% The msw for MU is indexed via mu(<term_name>/<arity>, <arg_position>)
%%
allbind(_, _, [], _) :- !. 

allbind(T, I, [Arg|Args], ID) :-
    (  var(Arg)            
    -> msw(mu(T, I), Arg) 
    ;  true
    ),                         
    I1 is I + 1,
    allbind(T, I1, Args, ID).

%%
% [generateID/2] generateID(Term, ID)
% ---
% Term := partially ground term
% ID := generated ID of term based on unground variable positions
% ---
% Generates an ID for a term by summation over the positions of the unground variables.
% IDs are generated to avoid some unwanted instances of tabling during explaination generation in PRISM.
%%
generateID(Term, 0) :- ground(Term), !.

generateID(Term, ID) :-
    Term =.. [_ | Args],
    length(Args, N),
    foreach( (Arg, I) in (Args, 1..N), ac(ID, 0),
        (  var(Arg)
        -> ID^1 is ID^0 + (I * N)
        ;  ID^1 is ID^0 
        )
    ).

%%------------------------------------
%% Declarations section
%%------------------------------------

%%
% [UPSILON] Prior distribution on starting states
%%
values(start, [start_r]).
values(start_r, [upsilon(start, 1), upsilon(start, 2)]).

rule(upsilon(start, 1), start, (null, com)).
rule(upsilon(start, 2), start, (null, mkdir(Dir, start))).

%%
% [DELTA] Abstract transitions
% ---
% Each ground state is first matched to the most specific abstract state,
%     then the transition is chosen based on transition probability,
%     returns the rule/3 which deterministically unifies with the selected transition (EX: delta(ls, 1)).
%
% values/2 for more general B should be placed below values/2 for more specific B, 
% implementing the subsumption constraint.
%%

% com
values(com, [com_r]).
values(com_r, [delta(com_r, 1), delta(com_r, 2), delta(com_r, 3)])

rule(delta(com_r, 1), com, (com, com)).
rule(delta(com_r, 1), com, (com, mkdir(_, com))).
rule(delta(com_r, 3), com, (com, stop)).

% mkdir(Dir, start)
values(mkdir(Dir, start), [mkdir_1_r]).
values(mkdir_1_r, [delta(mkdir_1_r, 1), delta(mkdir_1_r, 2), delta(mkdir_1_r, 3), delta(mkdir_1_r, 4), delta(mkdir_1_r, 5), delta(mkdir_1_r, 6), delta(mkdir_1_r, 7), delta(mkdir_1_r, 8), delta(mkdir_1_r, 9), delta(mkdir_1_r, 10), delta(mkdir_1_r, 11), delta(mkdir_1_r, 12), delta(mkdir_1_r, 13), delta(mkdir_1_r, 14)]).

rule(delta(mkdir_1_r, 1), mkdir(Dir, start), (mkdir(Dir), mkdir(Dir, com))).
rule(delta(mkdir_1_r, 2), mkdir(Dir, start), (mkdir(Dir), mkdir(_, com))).
rule(delta(mkdir_1_r, 3), mkdir(Dir, start), (mkdir(Dir), com)).
rule(delta(mkdir_1_r, 4), mkdir(Dir, start), (mkdir(Dir), ls(Dir, mkdir))).
rule(delta(mkdir_1_r, 5), mkdir(Dir, start), (mkdir(Dir), ls(_, mkdir))).
rule(delta(mkdir_1_r, 6), mkdir(Dir, start), (mkdir(Dir), cd(Dir, mkdir))).
rule(delta(mkdir_1_r, 7), mkdir(Dir, start), (mkdir(Dir), cd(_, mkdir))).
rule(delta(mkdir_1_r, 8), mkdir(Dir, start), (mkdir(Dir), cp(_, Dir, mkdir))).
rule(delta(mkdir_1_r, 9), mkdir(Dir, start), (mkdir(Dir), cp(Dir, _, mkdir))).
rule(delta(mkdir_1_r, 10), mkdir(Dir, start), (mkdir(Dir), cp(_, _, mkdir))).
rule(delta(mkdir_1_r, 11), mkdir(Dir, start), (mkdir(Dir), mv(_, Dir, mkdir))).
rule(delta(mkdir_1_r, 12), mkdir(Dir, start), (mkdir(Dir), mv(Dir, _, mkdir))).
rule(delta(mkdir_1_r, 13), mkdir(Dir, start), (mkdir(Dir), mv(_, _, mkdir))).
rule(delta(mkdir_1_r, 14), mkdir(Dir, start), (mkdir(Dir), stop)).

% mkdir(Dir, com)
values(mkdir(Dir, com), [mkdir_2_r]).
values(mkdir_2_r, [delta(mkdir_2_r, 1), delta(mkdir_2_r, 2), delta(mkdir_2_r, 3), delta(mkdir_2_r, 4), delta(mkdir_2_r, 5), delta(mkdir_2_r, 6), delta(mkdir_2_r, 7), delta(mkdir_2_r, 8), delta(mkdir_2_r, 9), delta(mkdir_2_r, 10), delta(mkdir_2_r, 11), delta(mkdir_2_r, 12), delta(mkdir_2_r, 13), delta(mkdir_2_r, 14)]).

rule(delta(mkdir_2_r, 1), mkdir(Dir, com), (mkdir(Dir), mkdir(Dir, com))).
rule(delta(mkdir_2_r, 2), mkdir(Dir, com), (mkdir(Dir), mkdir(_, com))).
rule(delta(mkdir_2_r, 3), mkdir(Dir, com), (mkdir(Dir), com)).
rule(delta(mkdir_2_r, 4), mkdir(Dir, com), (mkdir(Dir), ls(Dir, mkdir))).
rule(delta(mkdir_2_r, 5), mkdir(Dir, com), (mkdir(Dir), ls(_, mkdir))).
rule(delta(mkdir_2_r, 6), mkdir(Dir, com), (mkdir(Dir), cd(Dir, mkdir))).
rule(delta(mkdir_2_r, 7), mkdir(Dir, com), (mkdir(Dir), cd(_, mkdir))).
rule(delta(mkdir_2_r, 8), mkdir(Dir, com), (mkdir(Dir), cp(_, Dir, mkdir))).
rule(delta(mkdir_2_r, 9), mkdir(Dir, com), (mkdir(Dir), cp(Dir, _, mkdir))).
rule(delta(mkdir_2_r, 10), mkdir(Dir, com), (mkdir(Dir), cp(_, _, mkdir))).
rule(delta(mkdir_2_r, 11), mkdir(Dir, com), (mkdir(Dir), mv(_, Dir, mkdir))).
rule(delta(mkdir_2_r, 12), mkdir(Dir, com), (mkdir(Dir), mv(Dir, _, mkdir))).
rule(delta(mkdir_2_r, 13), mkdir(Dir, com), (mkdir(Dir), mv(_, _, mkdir))).
rule(delta(mkdir_2_r, 14), mkdir(Dir, com), (mkdir(Dir), stop)).

%%
% [MU] Grounding rules
% ---
% Each variable in the term has an associated probability over a typed domain.
% It is assumed that the arguments are distributed independently via naive Bayes assumption (wrt the term name).
% Variables which appear multiple times in a term will be bound once in allbind/4 
% and the value will be shared via unification.
%%

% mkdir/2
values(mu(mkdir/2, 1), [d1, d2, d3, d4]).